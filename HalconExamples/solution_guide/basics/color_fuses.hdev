<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* color_fuses.hdev: classify fuses by color</c>
<l>dev_update_window ('off')</l>
<c>* ****</c>
<c>* step: set up fuse properties and hue ranges</c>
<c>* ****</c>
<l>FuseColors := ['Orange','Red','Blue','Yellow','Green']</l>
<l>FuseTypes := [5,10,15,20,30]</l>
<c>* HueRanges: Orange 10-30, Red 0-10...</c>
<l>HueRanges := [10,30,0,10,125,162,30,64,96,128]</l>
<l>Count := 0</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 800, 600, 'black', WH)</l>
<l>while (Count &lt;= 4)</l>
<c>    * ****</c>
<c>    * step: acquire image</c>
<c>    * ****</c>
<l>    read_image (Image, 'color/color_fuses_0' + Count)</l>
<l>    dev_display (Image)</l>
<l>    set_tposition (WH, 12, 512)</l>
<l>    write_string (WH, 'color/color_fuses0' + Count + '.png')</l>
<c>    * ****</c>
<c>    * step: extract saturated hues</c>
<c>    * ****</c>
<l>    decompose3 (Image, Red, Green, Blue)</l>
<l>    trans_from_rgb (Red, Green, Blue, Hue, Saturation, Intensity, 'hsv')</l>
<l>    threshold (Saturation, Saturated, 60, 255)</l>
<l>    reduce_domain (Hue, Saturated, HueSaturated)</l>
<l>    for Fuse := 0 to |FuseTypes| - 1 by 1</l>
<c>        * ****</c>
<c>        * step: classify specific fuse</c>
<c>        * ****</c>
<l>        threshold (HueSaturated, CurrentFuse, HueRanges[Fuse * 2], HueRanges[Fuse * 2 + 1])</l>
<l>        connection (CurrentFuse, CurrentFuseConn)</l>
<l>        fill_up (CurrentFuseConn, CurrentFuseFill)</l>
<l>        select_shape (CurrentFuseFill, CurrentFuseSel, 'area', 'and', 6000, 20000)</l>
<l>        area_center (CurrentFuseSel, FuseArea, Row1, Column1)</l>
<l>        dev_set_color ('magenta')</l>
<l>        for i := 0 to |FuseArea| - 1 by 1</l>
<l>            set_tposition (WH, Row1[i], Column1[i])</l>
<l>            write_string (WH, FuseColors[Fuse] + ' ' + FuseTypes[Fuse] + ' Ampere')</l>
<l>        endfor</l>
<l>        set_tposition (WH, 24 * (Fuse + 1), 12)</l>
<l>        dev_set_color ('slate blue')</l>
<l>        write_string (WH, FuseColors[Fuse] + ' Fuses: ' + |FuseArea|)</l>
<l>    endfor</l>
<l>    stop ()</l>
<l>    Count := Count + 1</l>
<l>endwhile</l>
<l>dev_update_window ('on')</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
