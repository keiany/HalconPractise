<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* This example demonstrates a completeness check of colored game</c>
<c>* pieces using Euclidian classification. The training and application</c>
<c>* of the classifier is first shown on colored images and then on</c>
<c>* gray images.</c>
<c>* </c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 557, 416, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_set_color ('black')</l>
<l>dev_set_draw ('margin')</l>
<c>* </c>
<c>* Initialization</c>
<l>ImageRootName := 'color/color_pieces_0'</l>
<l>Regions := ['yellow','pink','blue','background']</l>
<l>Highlight := ['goldenrod','magenta','cyan']</l>
<l>gen_empty_obj (Classes)</l>
<c>* </c>
<l>for Mode := 0 to 1 by 1</l>
<l>    read_image (Image, ImageRootName + '0')</l>
<c>    * </c>
<c>    * Simulate gray image</c>
<l>    if (Mode == 1)</l>
<l>        rgb1_to_gray (Image, GrayImage)</l>
<l>        compose3 (GrayImage, GrayImage, GrayImage, Image)</l>
<l>        dev_display (Image)</l>
<l>        disp_message (WindowHandle, 'Train and apply the classes again on gray images', 'window', 12, 12, 'black', 'false')</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<c>    * </c>
<c>    * Colored images</c>
<l>    if (Mode == 0)</l>
<c>        * </c>
<c>        * Specify color classes</c>
<l>        for I := 1 to 4 by 1</l>
<l>            dev_display (Image)</l>
<l>            dev_display (Classes)</l>
<l>            disp_message (WindowHandle, ['Drag rectangle inside ' + Regions[I - 1] + ' color','Click right mouse button to confirm'], 'window', 24, 12, 'black', 'false')</l>
<l>            draw_rectangle1 (WindowHandle, Row1, Column1, Row2, Column2)</l>
<l>            gen_rectangle1 (Rectangle, Row1, Column1, Row2, Column2)</l>
<l>            concat_obj (Classes, Rectangle, Classes)</l>
<l>        endfor</l>
<l>    endif</l>
<c>    * </c>
<c>    * Train the specified color classes</c>
<l>    disp_message (WindowHandle, 'Training...', 'window', 100, 12, 'black', 'false')</l>
<l>    Radius := []</l>
<l>    Center := []</l>
<l>    Quality := []</l>
<l>    select_obj (Classes, Background, 4)</l>
<l>    gen_empty_region (Background)</l>
<l>    Circles := []</l>
<c>    * </c>
<c>    * Generate the classification clusters for each color</c>
<l>    for Class := 1 to 3 by 1</l>
<l>        select_obj (Classes, TrainClass, Class)</l>
<l>        learn_ndim_norm (TrainClass, Background, Image, 'euclid', 40, 0, Rad, Cen, Qua)</l>
<l>        Circles := [Circles,|Rad|]</l>
<l>        Radius := [Radius,Rad]</l>
<l>        Center := [Center,Cen]</l>
<l>    endfor</l>
<l>    RadiusDup := Radius</l>
<l>    CenterDup := Center</l>
<c>    * </c>
<c>    * Use the Euclidian classifier to test if each image</c>
<c>    * contains four game pieces of each color</c>
<l>    for J := 0 to 3 by 1</l>
<l>        Radius := RadiusDup</l>
<l>        Center := CenterDup</l>
<l>        read_image (Image, ImageRootName + J)</l>
<l>        if (Mode == 1)</l>
<l>            rgb1_to_gray (Image, GrayImage)</l>
<l>            compose3 (GrayImage, GrayImage, GrayImage, Image)</l>
<l>        endif</l>
<l>        dev_display (Image)</l>
<l>        disp_message (WindowHandle, 'Looking for 4 game pieces of each color...', 'window', 24, 12, 'black', 'false')</l>
<c>        * </c>
<c>        * Apply the trained classes to subsequent images</c>
<l>        for Class := 1 to 3 by 1</l>
<l>            dev_set_color (Highlight[Class - 1])</l>
<l>            class_ndim_norm (Image, ClassRegions, 'euclid', 'multiple', Radius[0:Circles[Class - 1] - 1] + 30, Center[0:Circles[Class - 1] * 3 - 1])</l>
<l>            connection (ClassRegions, ConnectedRegions)</l>
<l>            select_shape (ConnectedRegions, SelectedRegions, 'area', 'and', 400, 99999)</l>
<l>            count_obj (SelectedRegions, Number)</l>
<l>            if (Class &lt; 3)</l>
<l>                Radius := Radius[Circles[Class - 1]:|Radius| - 1]</l>
<l>                Center := Center[Circles[Class - 1] * 3:|Center| - 1]</l>
<l>            endif</l>
<l>            dev_display (SelectedRegions)</l>
<l>            dev_set_color ('black')</l>
<l>            OutString := Regions[Class - 1] + ': ' + Number + '   '</l>
<l>            dev_set_color ('green')</l>
<l>            disp_message (WindowHandle, Regions[Class - 1] + ': ' + Number, 'window', 24 + 30 * Class, 12, 'black', 'false')</l>
<l>            if (Number != 4)</l>
<l>                disp_message (WindowHandle, 'NotOK', 'window', 24 + 30 * Class, 120, 'red', 'false')</l>
<l>            else</l>
<l>                disp_message (WindowHandle, 'OK', 'window', 24 + 30 * Class, 120, 'green', 'false')</l>
<l>            endif</l>
<l>        endfor</l>
<l>        dev_set_line_width (2)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endfor</l>
<l>endfor</l>
<l>dev_clear_window ()</l>
<l>dev_display (Image)</l>
<l>Message := 'The game pieces cannot be classified reliable on'</l>
<l>Message[1] := 'gray images because the gray values of the'</l>
<l>Message[2] := 'game pieces cannot always be distinguished from'</l>
<l>Message[3] := 'the gray values of the background.'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
