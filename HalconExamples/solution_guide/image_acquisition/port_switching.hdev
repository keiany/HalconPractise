<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* This example program shows how to switch between ports dynamically.</c>
<c>* NOTE: The example is preconfigured for the BitFlow image acquisition interface</c>
<c>*       (2 Pulnix TM 6701 cameras, upper camera connector = port 0, lower = port 4).</c>
<c>*       Please adapt the marked lines for your own image acquisition device.</c>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* general configuration of HDevelop</c>
<l>set_system ('do_low_error', 'true')</l>
<l>dev_update_window ('off')</l>
<l>stop ()</l>
<c>* step 1: connect to the two boards</c>
<c>* -&gt; Please adapt the following lines for your own image acquisition device</c>
<l>AcqName := 'BitFlow'</l>
<l>Port0 := 0</l>
<l>Port1 := 1</l>
<l>open_framegrabber (AcqName, 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'default', 'default', 'default', 0, -1, AcqHandle)</l>
<l>stop ()</l>
<c>* step 2: open two correctly sized windows</c>
<l>grab_image (Image0, AcqHandle)</l>
<l>get_image_pointer1 (Image0, Pointer, Type, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width / 4, Height / 4, 'black', WindowHandle0)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_open_window (0, Width / 4 + 20, Width / 4, Height / 4, 'black', WindowHandle1)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>stop ()</l>
<c>* step 3: grab and display images (synchronously)</c>
<l>while (1)</l>
<l>    set_framegrabber_param (AcqHandle, 'port', Port0)</l>
<l>    dev_set_window (WindowHandle0)</l>
<l>    grab_image (Image0, AcqHandle)</l>
<l>    dev_display (Image0)</l>
<l>    set_framegrabber_param (AcqHandle, 'port', Port1)</l>
<l>    dev_set_window (WindowHandle1)</l>
<l>    grab_image (Image1, AcqHandle)</l>
<l>    dev_display (Image1)</l>
<l>endwhile</l>
<l>stop ()</l>
<c>* step 4: grab and display images (asynchronously)</c>
<c>* disable the automatic start of a new grab</c>
<l>set_framegrabber_param (AcqHandle, 'start_async_after_grab_async', 'disable')</l>
<c>* switch to camera 0 and grab a first image</c>
<l>set_framegrabber_param (AcqHandle, 'port', Port0)</l>
<l>grab_image_start (AcqHandle, -1)</l>
<l>dev_set_window (WindowHandle0)</l>
<l>grab_image_async (Image0, AcqHandle, -1)</l>
<l>dev_display (Image0)</l>
<l>while (1)</l>
<c>    * switch to camera 1 and start a new grab</c>
<l>    set_framegrabber_param (AcqHandle, 'port', Port1)</l>
<l>    grab_image_start (AcqHandle, -1)</l>
<c>    * meanwhile, process image 0</c>
<c>    * then get image from camera 1</c>
<l>    dev_set_window (WindowHandle1)</l>
<l>    grab_image_async (Image1, AcqHandle, -1)</l>
<l>    dev_display (Image1)</l>
<c>    * switch to camera 0 and start a new grab</c>
<l>    set_framegrabber_param (AcqHandle, 'port', Port0)</l>
<l>    grab_image_start (AcqHandle, -1)</l>
<c>    * meanwhile, process image 1</c>
<c>    * then get image from camera 0</c>
<l>    dev_set_window (WindowHandle0)</l>
<l>    grab_image_async (Image0, AcqHandle, -1)</l>
<l>    dev_display (Image0)</l>
<l>endwhile</l>
<l>stop ()</l>
<c>* clean up</c>
<l>dev_update_window ('on')</l>
<l>close_framegrabber (AcqHandle)</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_close_window ()</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
