<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* This example program creates a model ROI from the result of inspect_shape_model.</c>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* general configuration of HDevelop</c>
<l>dev_update_window ('off')</l>
<c>* image acquisition and window size</c>
<l>open_framegrabber ('File', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'default', 'board/board.seq', 'default', -1, 1, FGHandle)</l>
<l>grab_image (ModelImage, FGHandle)</l>
<l>get_image_pointer1 (ModelImage, Pointer, Type, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'white', WindowHandle)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_display (ModelImage)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<c>* colors and other settings for the visualization</c>
<l>dev_set_color ('yellow')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (2)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* -------------------  start of the application  ----------------</c>
<c>* step 1: create an ROI around the arrow</c>
<l>gen_rectangle1 (ROI, 361, 131, 406, 171)</l>
<l>dev_display (ROI)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 2: create a first model region</c>
<l>reduce_domain (ModelImage, ROI, ImageROI)</l>
<l>inspect_shape_model (ImageROI, ShapeModelImage, ShapeModelRegion, 1, 30)</l>
<l>smallest_rectangle1 (ShapeModelRegion, ModelRegionRow1, ModelRegionColumn1, ModelRegionRow2, ModelRegionColumn2)</l>
<l>dev_open_window (0, Width + 20, 3 * (ModelRegionColumn2 - ModelRegionColumn1 + 10), 3 * (ModelRegionRow2 - ModelRegionRow1 + 10), 'white', WindowHandleZoom)</l>
<l>dev_set_part (ModelRegionRow1 - 5, ModelRegionColumn1 - 5, ModelRegionRow2 + 5, ModelRegionColumn2 + 5)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (ShapeModelRegion)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 3: process the model region</c>
<l>dev_set_draw ('fill')</l>
<l>fill_up (ShapeModelRegion, FilledModelRegion)</l>
<l>opening_circle (FilledModelRegion, ROI, 3.5)</l>
<l>dev_display (ModelImage)</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_color ('cyan')</l>
<l>dev_display (ROI)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 4: create the final model</c>
<l>reduce_domain (ModelImage, ROI, ImageROI)</l>
<l>create_shape_model (ImageROI, 3, 0, rad(360), 'auto', 'none', 'use_polarity', 30, 15, ModelID)</l>
<l>inspect_shape_model (ImageROI, ShapeModelImage, ShapeModelRegion, 1, 30)</l>
<l>get_shape_model_contours (ShapeModel, ModelID, 1)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (ShapeModelRegion)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 3: find the object in other images</c>
<l>dev_close_window ()</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_set_color ('cyan')</l>
<l>for i := 1 to 20 by 1</l>
<l>    grab_image (SearchImage, FGHandle)</l>
<l>    dev_display (SearchImage)</l>
<l>    find_shape_model (SearchImage, ModelID, 0, rad(360), 0.75, 3, 0, 'least_squares', 0, 0.7, RowCheck, ColumnCheck, AngleCheck, Score)</l>
<l>    if (|Score| &gt; 0)</l>
<l>        for j := 0 to |Score| - 1 by 1</l>
<l>            vector_angle_to_rigid (0, 0, 0, RowCheck[j], ColumnCheck[j], AngleCheck[j], MovementOfObject)</l>
<l>            affine_trans_contour_xld (ShapeModel, ModelAtNewPosition, MovementOfObject)</l>
<l>            dev_display (ModelAtNewPosition)</l>
<l>        endfor</l>
<l>    endif</l>
<l>    if (i != 20)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    endif</l>
<l>    stop ()</l>
<l>endfor</l>
<c>* -------------------  end of the application  -----------------</c>
<c>* clean up</c>
<l>dev_update_window ('on')</l>
<l>clear_shape_model (ModelID)</l>
<l>close_framegrabber (FGHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
