<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* This example program searches for multiple instances of a security ring.</c>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* general configuration of HDevelop</c>
<l>dev_update_window ('off')</l>
<c>* image acquisition and window size</c>
<l>open_framegrabber ('File', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'default', 'rings/rings.seq', 'default', -1, 1, FGHandle)</l>
<l>grab_image (ModelImage, FGHandle)</l>
<l>get_image_pointer1 (ModelImage, Pointer, Type, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'white', WindowHandle)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_display (ModelImage)</l>
<c>* colors and other settings for the visualization</c>
<l>dev_set_color ('blue')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (2)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* -------------------  start of the application  ----------------</c>
<c>* step 1: select the model object</c>
<l>Row := 251</l>
<l>Column := 196</l>
<l>Radius := 103</l>
<l>gen_circle (ModelROI, Row, Column, Radius)</l>
<l>dev_display (ModelROI)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 2: inspect the model region</c>
<l>reduce_domain (ModelImage, ModelROI, ImageROI)</l>
<l>inspect_shape_model (ImageROI, ShapeModelImage, ShapeModelRegion, 1, 30)</l>
<l>dev_clear_window ()</l>
<l>dev_display (ShapeModelRegion)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 3: create the model</c>
<l>reduce_domain (ModelImage, ModelROI, ImageROI)</l>
<l>create_shape_model (ImageROI, 'auto', 0, rad(360), 'auto', 'none', 'use_polarity', 30, 10, ModelID)</l>
<l>get_shape_model_contours (ShapeModel, ModelID, 1)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 4: find the object in other images</c>
<l>for i := 1 to 7 by 1</l>
<l>    grab_image (SearchImage, FGHandle)</l>
<l>    dev_display (SearchImage)</l>
<l>    find_shape_model (SearchImage, ModelID, 0, rad(360), 0.6, 0, 0.55, 'least_squares', 0, 0.8, RowCheck, ColumnCheck, AngleCheck, Score)</l>
<l>    for j := 0 to |Score| - 1 by 1</l>
<l>        vector_angle_to_rigid (0, 0, 0, RowCheck[j], ColumnCheck[j], AngleCheck[j], MovementOfObject)</l>
<l>        affine_trans_contour_xld (ShapeModel, ModelAtNewPosition, MovementOfObject)</l>
<l>        dev_set_color ('cyan')</l>
<l>        dev_display (ModelAtNewPosition)</l>
<l>        dev_set_color ('blue')</l>
<l>        affine_trans_pixel (MovementOfObject, -120, 0, RowArrowHead, ColumnArrowHead)</l>
<l>        disp_arrow (WindowHandle, RowCheck[j], ColumnCheck[j], RowArrowHead, ColumnArrowHead, 2)</l>
<l>    endfor</l>
<l>    disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    stop ()</l>
<l>endfor</l>
<c>* -------------------  end of the application  -----------------</c>
<c>* clean up</c>
<l>dev_update_window ('on')</l>
<l>clear_shape_model (ModelID)</l>
<l>close_framegrabber (FGHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
