<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* This example program creates a synthetic model in form of a circle to</c>
<c>* search for capacitors on a board.</c>
<c>* ------------------------------------------------------------------------------------------------</c>
<c>* general configuration of HDevelop</c>
<l>dev_update_window ('off')</l>
<c>* image acquisition and window size</c>
<l>open_framegrabber ('File', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'default', 'capacitors/capacitors.seq', 'default', -1, 1, FGHandle)</l>
<l>grab_image (ModelImage, FGHandle)</l>
<l>get_image_pointer1 (ModelImage, Pointer, Type, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowHandle)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_display (ModelImage)</l>
<c>* colors and other settings for the visualization</c>
<l>dev_set_color ('cyan')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (2)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* -------------------  start of the application  ----------------</c>
<c>* step 1: create a circle with the correct size</c>
<l>RadiusCircle := 43</l>
<l>SizeSynthImage := 2 * RadiusCircle + 10</l>
<l>gen_ellipse_contour_xld (Circle, SizeSynthImage / 2, SizeSynthImage / 2, 0, RadiusCircle, RadiusCircle, 0, 6.28318, 'positive', 1.5)</l>
<l>dev_display (Circle)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<c>* step 2: create an image and insert the XLD</c>
<l>dev_open_window (0, Width + 20, SizeSynthImage, SizeSynthImage, 'white', WindowHandleSynthetic)</l>
<l>dev_set_part (0, 0, SizeSynthImage - 1, SizeSynthImage - 1)</l>
<l>gen_image_const (EmptyImage, 'byte', SizeSynthImage, SizeSynthImage)</l>
<l>paint_xld (Circle, EmptyImage, SyntheticModelImage, 128)</l>
<l>dev_display (SyntheticModelImage)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 3: create the model</c>
<l>inspect_shape_model (SyntheticModelImage, ShapeModelImage, ShapeModelRegion, 1, 30)</l>
<l>dev_clear_window ()</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (ShapeModelRegion)</l>
<l>create_scaled_shape_model (SyntheticModelImage, 'auto', 0, 0, 0.01, 0.8, 1.2, 'auto', 'none', 'use_polarity', 30, 10, ModelID)</l>
<l>get_shape_model_contours (ShapeModel, ModelID, 1)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* step 4: find the object in other images</c>
<l>dev_set_window (WindowHandleSynthetic)</l>
<l>dev_close_window ()</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>dev_set_color ('yellow')</l>
<l>dev_set_line_width (2)</l>
<l>for i := 1 to 3 by 1</l>
<l>    grab_image (SearchImage, FGHandle)</l>
<l>    dev_display (SearchImage)</l>
<l>    find_scaled_shape_model (SearchImage, ModelID, 0, 0, 0.8, 1.2, 0.7, 0, 0.5, 'least_squares', 3, 0, RowCheck, ColumnCheck, AngleCheck, ScaleCheck, Score)</l>
<l>    if (|Score| &gt; 0)</l>
<l>        for j := 0 to |Score| - 1 by 1</l>
<l>            vector_angle_to_rigid (0, 0, 0, RowCheck[j], ColumnCheck[j], AngleCheck[j], MovementOfObject)</l>
<l>            hom_mat2d_scale (MovementOfObject, ScaleCheck[j], ScaleCheck[j], RowCheck[j], ColumnCheck[j], MoveAndScaleOfObject)</l>
<l>            affine_trans_contour_xld (ShapeModel, ModelAtNewPosition, MoveAndScaleOfObject)</l>
<l>            dev_display (ModelAtNewPosition)</l>
<l>        endfor</l>
<l>    endif</l>
<l>    disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    stop ()</l>
<l>endfor</l>
<c>* -------------------  end of the application  -----------------</c>
<c>* clean up</c>
<l>clear_shape_model (ModelID)</l>
<l>dev_update_window ('on')</l>
<l>close_framegrabber (FGHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
