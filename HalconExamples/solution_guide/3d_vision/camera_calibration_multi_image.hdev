<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.1.0">
<procedure name="main">
<interface/>
<body>
<l>ImgPath := '3d_machine_vision/calib/'</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 640, 480, 'black', WindowHandle)</l>
<l>dev_update_off ()</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (3)</l>
<l>set_display_font (WindowHandle, 22, 'mono', 'true', 'false')</l>
<c>* </c>
<c>* Calibrate the camera.</c>
<c>* </c>
<l>gen_cam_par_area_scan_division (0.012, 0, 0.00000375, 0.00000375, 640, 480, 1280, 960, StartCamPar)</l>
<l>create_calib_data ('calibration_object', 1, 1, CalibDataID)</l>
<l>set_calib_data_cam_param (CalibDataID, 0, [], StartCamPar)</l>
<l>set_calib_data_calib_object (CalibDataID, 0, 'calplate_80mm.cpd')</l>
<l>NumImages := 7</l>
<l>for I := 1 to NumImages by 1</l>
<l>    read_image (Image, ImgPath + 'calib_image_' + I$'02d')</l>
<l>    dev_display (Image)</l>
<l>    find_calib_object (Image, CalibDataID, 0, 0, I, [], [])</l>
<l>    get_calib_data_observ_contours (Caltab, CalibDataID, 'caltab', 0, 0, I)</l>
<l>    get_calib_data_observ_points (CalibDataID, 0, 0, I, Row, Column, Index, StartPose)</l>
<l>    dev_set_color ('green')</l>
<l>    dev_display (Caltab)</l>
<l>    dev_set_color ('red')</l>
<l>    disp_circle (WindowHandle, Row, Column, gen_tuple_const(|Row|,1.5))</l>
<l>endfor</l>
<l>calibrate_cameras (CalibDataID, Errors)</l>
<l>get_calib_data (CalibDataID, 'camera', 0, 'params', CamParam)</l>
<c>* The reference image, i.e., the image in which the calibration</c>
<c>* plate is located on the ruler is the first image</c>
<l>get_calib_data (CalibDataID, 'calib_obj_pose', [0,1], 'pose', Pose)</l>
<c>* To take the thickness of the calibration plate into account, the z-value</c>
<c>* of the origin given by the camera pose has to be translated by the</c>
<c>* thickness of the calibration plate.</c>
<c>* Deactivate the following line if you do not want to add the correction.</c>
<l>set_origin_pose (Pose, 0, 0, 0.002, Pose)</l>
<c>* measure the distance between the pitch lines</c>
<l>read_image (Image, ImgPath + 'ruler')</l>
<l>dev_display (Image)</l>
<l>gen_measure_rectangle2 (690, 680, rad(-0.25), 480, 8, 1280, 960, 'bilinear', MeasureHandle)</l>
<l>measure_pairs (Image, MeasureHandle, 0.5, 5, 'all', 'all', RowEdgeFirst, ColumnEdgeFirst, AmplitudeFirst, RowEdgeSecond, ColumnEdgeSecond, AmplitudeSecond, IntraDistance, InterDistance)</l>
<l>Row := (RowEdgeFirst + RowEdgeSecond) / 2.0</l>
<l>Col := (ColumnEdgeFirst + ColumnEdgeSecond) / 2.0</l>
<l>disp_cross (WindowHandle, Row, Col, 20, rad(45))</l>
<l>image_points_to_world_plane (CamParam, Pose, Row, Col, 'mm', X1, Y1)</l>
<l>distance_pp (X1[0:11], Y1[0:11], X1[1:12], Y1[1:12], Distance)</l>
<l>tuple_mean (Distance, MeanDistance)</l>
<l>tuple_deviation (Distance, DeviationDistance)</l>
<l>disp_message (WindowHandle, 'Mean distance: ' + MeanDistance$'.3f' + 'mm +/- ' + DeviationDistance$'.3f' + 'mm', 'window', 30, 60, 'yellow', 'false')</l>
<c></c>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
